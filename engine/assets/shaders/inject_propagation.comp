#version 450
#extension GL_ARB_shader_image_load_store : require
layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(binding = 0, rgba8) uniform readonly image3D voxelAlbedo;
layout(binding = 1, rgba8) uniform readonly image3D voxelNormal;
layout(binding = 2, rgba8) uniform writeonly image3D voxelComposite;
layout(binding = 3, rgba8) uniform sampler3D voxelTex;
layout(binding = 4, rgba8) uniform sampler3D voxelTexMipmap[6];

uvec3 volumeDimension;

const float PI = 3.14159265f;
uniform float maxTracingDistanceGlobal = 1.0f;

// four cones
const vec3 propagationDirections[] =
{
    vec3(0.0f, 1.0f, 0.0f),
    vec3(0.0f, 0.5f, 0.866025f),
    vec3(0.754996f, 0.5f, -0.4330128f),
    vec3(-0.754996f, 0.5f, -0.4330128f)
};

// six cones
// const vec3 propagationDirections[] =
// {
//     vec3(0.0f, 1.0f, 0.0f),
//     vec3(0.0f, 0.5f, 0.866025f),
//     vec3(0.823639f, 0.5f, 0.267617f),
//     vec3(0.509037f, 0.5f, -0.7006629f),
//     vec3(-0.50937f, 0.5f, -0.7006629f),
//     vec3(-0.823639f, 0.5f, 0.267617f)
// };

const float diffuseConeWeights[] =
{
    PI / 4.0f,
    3.0f * PI / 20.0f,
    3.0f * PI / 20.0f,
    3.0f * PI / 20.0f,
    3.0f * PI / 20.0f,
    3.0f * PI / 20.0f,
};

vec4 convRGBA8ToVec4(uint val)
{
    return vec4(float((val & 0x000000FF)), 
    float((val & 0x0000FF00) >> 8U), 
    float((val & 0x00FF0000) >> 16U), 
    float((val & 0xFF000000) >> 24U));
}

vec4 TraceCone(vec3 position, vec3 direction, float aperture, float maxTracingDistance)
{
    uvec3 visibleFace;
    visibleFace.x = (direction.x < 0.0) ? 0 : 1;
    visibleFace.y = (direction.y < 0.0) ? 2 : 3;
    visibleFace.z = (direction.z < 0.0) ? 4 : 5;
    // weight per axis for aniso sampling
    vec3 weight = abs(direction);
    // navigation
    float voxelSize = 1.0f / volumeDimension.z;
    // move one voxel further to avoid self collision
    float dst = voxelSize * 2.0f;
    float diameter = aperture * dst;
    vec3 samplePos = direction * dst + position;
    // control vars
    float mipLevel = 0.0f;
    float anisoLevel = 0.0f;
    float mipMaxLevel = log2(volumeDimension.z) - 1;
    // accumulated sample
    vec4 coneSample = vec4(0.0f);
    vec4 baseColor = vec4(0.0f);
    vec4 anisoSample = vec4(0.0f);

    while(coneSample.a <= 1.0f && dst <= maxTracingDistance && dst <= maxTracingDistanceGlobal)
    {
        // outisde bounds
        if (aperture < 0.3f && (samplePos.x < 0.0f || samplePos.y < 0.0f || samplePos.z < 0.0f
            || samplePos.x > 1.0f || samplePos.y > 1.0f || samplePos.z > 1.0f)) 
        { 
            break; 
        }

        mipLevel = clamp(log2(diameter * volumeDimension.z), 0.0f, mipMaxLevel);
        anisoLevel = max(mipLevel - 1.0f, 0.0f);
        // aniso sampling
        anisoSample = weight.x * textureLod(voxelTexMipmap[visibleFace.x], samplePos, anisoLevel)
                    + weight.y * textureLod(voxelTexMipmap[visibleFace.y], samplePos, anisoLevel)
                    + weight.z * textureLod(voxelTexMipmap[visibleFace.z], samplePos, anisoLevel);

       if(mipLevel < 1.0f)
        {
            baseColor = texture(voxelTex, samplePos);
            anisoSample = mix(baseColor, anisoSample, clamp(mipLevel, 0.0f, 1.0f));
        }
        // accumulate sampling
        coneSample += (1.0f - coneSample.a) * anisoSample;
        // move further into volume
        dst += max(diameter, voxelSize);
        diameter = dst * aperture;
        samplePos = direction * dst + position;
    }

    return coneSample;
}

vec4 CalculateIndirectLighting(vec3 position, vec3 normal)
{
    vec4 diffuseTrace = vec4(0.0f);

    // diffuse cone setup
    float aperture = 0.7853f;
    vec3 up = (normal.y * normal.y) > 0.95f ? vec3(0.0f, 0.0f, 1.0f) : vec3(0.0f, 1.0f, 0.0f);
    vec3 right = cross(normal, up);
    up = cross(normal, right);

    for(int i = 0; i < 4; i++)
    {
        vec3 coneDirection = normal;
        coneDirection += propagationDirections[i].x * right + propagationDirections[i].z * up;
        coneDirection = normalize(coneDirection);

        diffuseTrace += TraceCone(position, coneDirection, aperture, 1.0f) * diffuseConeWeights[i];
    }

    return clamp(diffuseTrace, 0.0f, 1.0f);
}

void main()
{
    volumeDimension = imageSize(voxelComposite);
    float dimension = float(volumeDimension);

    if(gl_GlobalInvocationID.x >= volumeDimension.z ||
        gl_GlobalInvocationID.y >= volumeDimension.z ||
        gl_GlobalInvocationID.z >= volumeDimension.z) return;

    ivec3 writePos = ivec3(gl_GlobalInvocationID);
    vec4 albedo = imageLoad(voxelAlbedo, writePos);

    if(albedo.a == 0.0f) { return; }

    vec4 injectionPass = texelFetch(voxelTex, writePos, 0);
    // normal from voxelization
    vec4 normal = imageLoad(voxelNormal, writePos);
    // normal is stored in 0-1 range, restore to -1 -> -1
    normal = normalize(normal * 2.0f - 1.0f);
    // calculate indirect lighting - first bounce onto the voxel texture
    vec3 position = writePos / dimension;
    vec4 indirectLighting = CalculateIndirectLighting(position, normal.xyz);
    // first bounce gi component only
    vec4 firstBounce = indirectLighting * albedo;
    // first bounce + direct lighting
    vec4 radiance = injectionPass + firstBounce;
    radiance.a = 1.0f;

    imageStore(voxelComposite, writePos, clamp(radiance, 0.0f, 1.0f));
}