#version 430
layout (local_size_x = 24, local_size_y = 4, local_size_z = 4) in;

layout(binding = 0, rgba8) uniform readonly image3D voxelMipmapSrc;
layout(binding = 1, rgba8) uniform writeonly image3D voxelMipmapDst;

uniform vec3 mipDimension;

const ivec3 mipOffsets[] = ivec3[8]
(
	ivec3(1, 1, 1),
	ivec3(1, 1, 0),
	ivec3(1, 0, 1),
	ivec3(1, 0, 0),
	ivec3(0, 1, 1),
	ivec3(0, 1, 0),
	ivec3(0, 0, 1),
	ivec3(0, 0, 0)
);

void FetchTexels(ivec3 pos, inout vec4 val[8]) 
{
	for(int i = 0; i < 8; i++)
	{
		val[i] = imageLoad(voxelMipmapSrc, pos + mipOffsets[i]);
	}
}

void main()
{
	if(gl_GlobalInvocationID.x >= mipDimension.x ||
		gl_GlobalInvocationID.y >= mipDimension.y ||
		gl_GlobalInvocationID.z >= mipDimension.z) return;

	ivec3 writePos = ivec3(gl_GlobalInvocationID);
	ivec3 sourcePos = writePos * 2;
	int writeDirection = int(writePos.x / mipDimension.z);
	// fetch values
	vec4 values[8];
	FetchTexels(sourcePos, values);

	// x -
	if(writeDirection == 0)
		imageStore(voxelMipmapDst, writePos, 
		(
			values[0] + values[4] * (1 - values[0].a) + 
			values[1] + values[5] * (1 - values[1].a) +
			values[2] + values[6] * (1 - values[2].a) +
			values[3] + values[7] * (1 - values[3].a)) * 0.25f
		);
	// x +
	else if(writeDirection == 1)	
	    imageStore(voxelMipmapDst, writePos, 
		(
			values[4] + values[0] * (1 - values[4].a) +
	    	values[5] + values[1] * (1 - values[5].a) +
	    	values[6] + values[2] * (1 - values[6].a) +
	    	values[7] + values[3] * (1 - values[7].a)) * 0.25f
	    );
	// y -	
	else if(writeDirection == 2)
	    imageStore(voxelMipmapDst, writePos, 
		(
			values[0] + values[2] * (1 - values[0].a) +
	    	values[1] + values[3] * (1 - values[1].a) +
	    	values[5] + values[7] * (1 - values[5].a) +
	    	values[4] + values[6] * (1 - values[4].a)) * 0.25f
	    );
	// y +
	else if(writeDirection == 3)
	    imageStore(voxelMipmapDst, writePos, 
		(
			values[2] + values[0] * (1 - values[2].a) +
	    	values[3] + values[1] * (1 - values[3].a) +
	    	values[7] + values[5] * (1 - values[7].a) +
	    	values[6] + values[4] * (1 - values[6].a)) * 0.25f
	    );
	// z -
	else if(writeDirection == 4)
	    imageStore(voxelMipmapDst, writePos, 
		(
			values[0] + values[1] * (1 - values[0].a) +
	    	values[2] + values[3] * (1 - values[2].a) +
	    	values[4] + values[5] * (1 - values[4].a) +
	    	values[6] + values[7] * (1 - values[6].a)) * 0.25f
	    );
	// z +
	else if(writeDirection == 5)
	    imageStore(voxelMipmapDst, writePos, 
		(
			values[1] + values[0] * (1 - values[1].a) +
	    	values[3] + values[2] * (1 - values[3].a) +
	    	values[5] + values[4] * (1 - values[5].a) +
	    	values[7] + values[6] * (1 - values[7].a)) * 0.25f
	    );
}